import os
Import('env')

#
# Need to make a copy because SCons uses the environment
# at it's final state, so StGermain ends up depending on
# StgDomain, etc. 
#

env = env.Clone()

#
# Inside each project we will be accessing headers without the
# project name as a prefix, so we need to let SCons know how to
# find those headers.
#

env.Append(CPPPATH=env['build_dir'] + '/include/Geothermal')

#
# Need to include the Geothermal library for binaries.
#

libs=[]
for lib in ['Geothermal'] + env.get('LIBS', []):
    if lib != "Experimental":
        libs.append(lib)
#
# Keep a list of all the objects we build so we can make a library
# afterwards.
#

objs = []
suites = []
tst_exp = []
tst_input = []

# Get the revision definition.
revision = "UNKNOWN"
path = "UNKNOWN"
branch = "UNKNOWN"
for entry in env['IDDEFINES']:
    if entry[0] == 'GEOTHERMAL':
        revision = entry[1]
        path = entry[2]
        branch = entry[3]
        break

#
# Process each directory uniformly.
#

dirs = Split('Base libGeothermal')
for d in dirs:

    # Need the module name, which is just the directory.
    mod_name = env['ESCAPE']('"' + ''.join(d.split('/')) + '"')
    cpp_defs = [('CURR_MODULE_NAME', mod_name)] + env.get('CPPDEFINES', []) + \
               [('VERSION', revision)] + [('PATH', path)] + \
               [('BRANCH', branch)]

    # Setup where to look for files.
    src_dir = d + '/src'
    inc_dir = 'include/Geothermal/' + d
    tst_dir = d + '/tests'
    tst_exp_dir = tst_dir + '/expected'
    tst_input_dir = tst_dir + '/input'
    tst_install_dir = 'tests/Geothermal/' + d

    # Install the headers and '.def' files.
    hdrs = env.Install(inc_dir, Glob(src_dir + '/*.h'))
    defs = env.Install(inc_dir, Glob(src_dir + '/*.def'))

    # Build our source files.
    srcs = Glob(src_dir + '/*.c')
    for src in srcs:
        stg_cpp_defs = cpp_defs + [('SOURCE_FILE', '"\\"' + src.name + '\\""')]
        objs += env.SharedObject(src, CPPDEFINES=stg_cpp_defs)

    # Build any test suites we might find.
    suites += env.Object(Glob(tst_dir + '/*Suite.c'))

    # Install any test expected and input files
    tst_exp += env.Install(tst_install_dir + '/expected', Glob(tst_exp_dir + '/*'))
    tst_input += env.Install(tst_install_dir + '/input', Glob(tst_input_dir + '/*'))

# Need to install headers from libGeothermal.
env.Install('include/Geothermal', Glob('libGeothermal/src/*.h'))

#
# Build plugins.
#
dirs = ['libGeothermal/Toolbox',
	'plugins/ResidualEvaluation',
   #'plugins/HeatBudget',
	'plugins/DarcyConditionFunction',
	'plugins/GOCADOutput',
	'plugins/TemperaturePointCloud',
	'plugins/DepthMap',
	'plugins/FieldMaps',
	'plugins/DrillCores']
pl_objs = []
pl_regs = []
for d in dirs:

    name = 'Geothermal_' + d.split('/')[-1] + 'module'
    mod_name = env['ESCAPE']('"' + ''.join(d.split('/')) + '"')
    cpp_defs = [('CURR_MODULE_NAME', mod_name)] + env.get('CPPDEFINES', []) + \
               [('VERSION', revision)] + [('PATH', path)] + \
               [('BRANCH', branch)]

    env.Install('include/Geothermal/' + d.split('/')[-1], Glob(d + '/*.h'))

    srcs = Glob(d + '/*.c')
    cur_objs = env.SharedObject(srcs, CPPDEFINES=cpp_defs)

    # If we have shared libraries, build the dynamic plugin.
    if env['shared_libs']:
        lib_pre = env['LIBPREFIXES']
        if not isinstance(lib_pre, list):
            lib_pre = [lib_pre]
        env.SharedLibrary('lib/' + name, cur_objs,
                          SHLIBPREFIX='',
                          LIBPREFIXES=lib_pre + [''],
                          LIBS=libs)

    # If we are building static libs we need to construct a C file
    # mapping the plugin's name to its register function.
    if env['static_libs']:
        pl_regs += [name]

    # Keep track of all the plugin objects.
    pl_objs += cur_objs

#
# Build shared library.
#

if env['shared_libs']:
    env.SharedLibrary('lib/Geothermal', objs)

#
# Write the static registry code.
#

if env['static_libs']:

    reg_c = '#include <StGermain/StGermain.h>\n\n'
    reg_c += 'extern int stg_num_modules;\n'
    reg_c += 'extern char **stg_module_names;\n'
    reg_c += 'extern int *stg_num_module_syms;\n'
    reg_c += 'extern char ***stg_module_syms;\n\n'
    reg_c += 'extern void ***stg_module_funcs;\n\n'
    for n in pl_regs:
        n = n[:-6]
        reg_c += 'extern void (%s_GetName)();\n'%n
        reg_c += 'extern void (%s_Register)();\n'%n
        if n.find('Toolbox') != -1:
            reg_c += 'extern void (%s_Initialise)();\n'%n
            reg_c += 'extern void (%s_Finalise)();\n'%n
    reg_c += '\n'

    reg_c += 'void geothermal_register_static_modules() {\n'
    reg_c += '   int new_num = stg_num_modules + %d;\n\n'%len(pl_regs)
    
    reg_c += '   stg_module_names = (char**)realloc(stg_module_names, new_num*sizeof(char*));\n'
    for i in range(len(pl_regs)):
        n = pl_regs[i][:-6]
        reg_c += '   stg_module_names[stg_num_modules + %d] = (char*)"%s";\n'%(i, n)
        
    reg_c += '\n   stg_num_module_syms = (int*)realloc(stg_num_module_syms, new_num*sizeof(int));\n'
    for i in range(len(pl_regs)):
        n = pl_regs[i][:-6]
        if n.find('Toolbox') != -1:
            num_syms = 5
        else:
            num_syms = 1
        reg_c += '   stg_num_module_syms[stg_num_modules + %d] = %d;\n'%(i, num_syms)

    reg_c += '\n   stg_module_syms = (char***)realloc(stg_module_syms, new_num*sizeof(char**));\n'
    for i in range(len(pl_regs)):
        n = pl_regs[i][:-6]
        if n.find('Toolbox') != -1:
            num_syms = 5
        else:
            num_syms = 1
        reg_c += '   stg_module_syms[stg_num_modules + %d] = (char**)malloc(%d*sizeof(char*));\n'%(i, num_syms)
        reg_c += '   stg_module_syms[stg_num_modules + %d][0] = (char*)"%s_Register";\n'%(i, n)
        if n.find('Toolbox') != -1:
            reg_c += '   stg_module_syms[stg_num_modules + %d][2] = (char*)"%s_GetName";\n'%(i, n)
            reg_c += '   stg_module_syms[stg_num_modules + %d][3] = (char*)"%s_Initialise";\n'%(i, n)
            reg_c += '   stg_module_syms[stg_num_modules + %d][4] = (char*)"%s_Finalise";\n'%(i, n)

    reg_c += '\n   stg_module_funcs = (void***)realloc(stg_module_funcs, new_num*sizeof(void**));\n'
    for i in range(len(pl_regs)):
        n = pl_regs[i][:-6]
        if n.find('Toolbox') != -1:
            num_syms = 5
        else:
            num_syms = 1
        reg_c += '   stg_module_funcs[stg_num_modules + %d] = (void**)malloc(%d*sizeof(void*));\n'%(i, num_syms)
        reg_c += '   stg_module_funcs[stg_num_modules + %d][0] = (void*)%s_Register;\n'%(i, n)
        if n.find('Toolbox') != -1:
            reg_c += '   stg_module_funcs[stg_num_modules + %d][2] = (void*)%s_GetName;\n'%(i, n)
            reg_c += '   stg_module_funcs[stg_num_modules + %d][3] = (void*)%s_Initialise;\n'%(i, n)
            reg_c += '   stg_module_funcs[stg_num_modules + %d][4] = (void*)%s_Finalise;\n'%(i, n)

    reg_c += '\n   stg_num_modules += %d;\n'%len(pl_regs)
    reg_c += '}\n'

    reg_filename = os.path.join(env['build_dir'], 'Geothermal', 'geothermal_static_modules.c')
    if not os.path.exists(os.path.dirname(reg_filename)):
        os.makedirs(os.path.dirname(reg_filename))
    reg_file = open(reg_filename, 'w')
    reg_file.write(reg_c)
    reg_file.close()
    reg_obj = env.Object(reg_filename)

    # Add our register function to the StGermain module file.
    f = open(File(env['build_dir'] + '/StGermain/stg_static_modules.c').abspath, 'r')
    txt = f.readlines()
    f.close()
    txt.insert(-2, '   geothermal_register_static_modules();\n')
    txt.insert(0, 'void geothermal_register_static_modules();\n')
    f = open(File(env['build_dir'] + '/StGermain/stg_static_modules.c').abspath, 'w')
    f.writelines(txt)
    f.close()

    # Static library.
    l = env.StaticLibrary(env['build_dir'] + '/lib/Geothermal', objs + pl_objs + reg_obj)
    env.Install(env['prefix'] + '/lib', l)


#
# Test runner program.
#

env.PCUTest('tests/testGeothermal', 
    suites,
    PCU_LIBHEADERS="#include <StGermain/StGermain.h>\n#include <StgDomain/StgDomain.h>\n"\
        "#include <StgFEM/StgFEM.h>\n#include <PICellerator/PICellerator.h>\n"\
        "#include <Underworld/Underworld.h>\n"\
        "#include <Geothermal/Geothermal.h>\n",
    PCU_SETUP="StGermain_Init(&argc, &argv);\nModulesManager_LoadModule(stgToolboxesManager, \"Geothermal\" );\n",
    PCU_TEARDOWN="StGermain_Finalise();",
    LIBS=libs, 
    PCU_EXP=tst_exp, 
    PCU_INPUT=tst_input, 
    PROJECT="Geothermal" )

#
# Install XML input files.
#

env.Install('lib/StGermain/Geothermal', Glob('InputFiles/geothermal_components/*.xml'))
env.Install('lib/StGermain/Geothermal/Milestone1', Glob('InputFiles/gsv_milestone1/*.xml'))
env.Install('lib/StGermain/Geothermal/Milestone2', Glob('InputFiles/gsv_milestone2/*.xml'))
env.Install('lib/StGermain/Geothermal/Milestone3', Glob('InputFiles/gsv_milestone3/*.xml'))
env.Install('lib/StGermain/Geothermal/Milestone4', Glob('InputFiles/gsv_milestone4/*.xml'))
